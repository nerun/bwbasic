1 REM DEPRDDB
5 CLS
10 PRINT "    DEPRECIATION SCHEDULE-SINKING FUND METHOD"
20 PRINT "THIS PROGRAM PRINTS A SCHEDULE FOR DEPRECIATING AN ASSET
30 PRINT"BY THE SINKING FUND METHOD. THIS METHOD IS USED BY REGULATED
33 PRINT"INDUSTRIES QUITE A BIT. IN THIS METHOD THERE ARE EQUAL YEARLY
35 PRINT"DEPRECIATION CHARGES BUT THE DEPRECIATION IS ASSUMED TO BE
37 PRINT"INVESTED IN A (SINKING FUND) ACCOUNT EARNING STATED INTEREST.
40 PRINT "INPUTS ARE ITEM COST,ESTIMATED SALVAGE VALUE, LIFE IN YEARS,
45 PRINT "AND SINKING FUND INTEREST RATE."
50 PRINT:PRINT"OUTPUT INCLUDES ANNUAL DEPRECIATION CHARGES PLUS A YEAR BY
60 PRINT"YEAR SCHEDULE OF BOOK VALUE,INTEREST ON SINKING FUND, INCREASE
70 PRINT"IN SIZE OF SINKING FUND, AND TOTAL VALUE OF SINKING FUND.
80 PRINT
90 INPUT " PRESS ENTER TO BEGIN";Z9
100 CLS
105 INPUT"DO YOU WANT OUTPUT ON LINEPRINTER(Y/N)";Z2$
106 IF Z2$<>"Y" AND Z2$<>"N" THEN GOTO 105
110 INPUT "ENTER  COST              ";C
120 INPUT "ENTER SALVAGE VALUE      ";S
130 INPUT "ENTER LIFE IN YEARS      ";L
135 INPUT"ENTER INT RATE ON SINKING FUND(%)";IR:IR=IR/100
140 IF C >= S THEN  GOTO 200
150 PRINT
160 PRINT"SALVAGE VALUE > COST  ---- ERROR-ERROR"
170 PRINT"ENTER DATA AGAIN."
180 FOR I= 1 TO 400:NEXT I
190 GOTO 100
200 CLS
210 GOSUB 1000:REM HEADING
220 GOSUB 1100:REM INITIALIZE
230 GOSUB 1200:REM COMPUTE & PRINT
250 END
1000 REM HEADING SUBROUTINE
1010 CLS
1015 IF Z2$="Y" THEN INPUT"PRESS ENTER WHEN PRINTER READY";Z9$:PRINT
1020 H1$="YEAR    BOOK VAL.    FUND INT.   INCR. IN FUND   FUND VAL.
1030 H2$=" ##    #######.##   #######.##    #######.##    #######.##"
1037 IF Z2$="Y" THEN LPRINT "SINKING FUND DEPRECIATION SCHEDULE":LPRINT" ":LPRINT "INITIAL COST = ";C:LPRINT "SALVAGE VALUE = ";S:LPRINT"LIFE OF ASSET(YEARS) =";L
1038 IF Z2$="Y" THEN LPRINT"INTEREST RATE ON SINKING FUND = ";IR*100
1039 D=(C-S)*(IR/((1+IR)[L-1)):PRINT "ANNUAL DEPRECIATION CHARGES = ";INT(100*D+.5)/100:IF Z2$="Y" THEN LPRINT"ANNUAL DEPRECIATION CHARGE = ";INT(100*D+.5)/100
1040 PRINT H1$:IF Z2$="Y" THEN LPRINT H1$:LPRINT " "
1050 RETURN
1100 REM INITIALIZE SUBROUTINE
1110 AD=0:REM ACCUMULATED DEPRECIATION
1115 DP=C-S :REM AMOUNT TO BE DEPRECIATED
1120 BV=C :REM BOOK VALUE
1125 YR=0 :REM PRESENT YEAR
1150 RETURN
1200 REM COMPUTATION AND PRINTING SUBROUTINE
1210 PRINT USING H2$;YR,C
1215 IF Z2$="Y" THEN LPRINT USING H2$;YR,C:LPRINT" "
1217 BV=C
1220 FOR I=1 TO L
1230 FI=FV*IR:REM FUND INTEREST FOR YEAR
1232 FC=FI+D:REM FUND VALUE INCREASE
1234 FV=FV+FC:REM NEW FUND VALUE
1240 BV=BV-FC
1250 YR=YR+1
1260 PRINT USING H2$;YR,BV,FI,FC,FV
1262 IF Z2$="Y" THEN LPRINT USING H2$;YR,BV,FI,FC,FV:GOTO 1270
1265 IF I/10=INT(I/10) THEN GOSUB 1400 ' STOP SCROLLING OFF SCREEN
1270 NEXT I
1300 RETURN
1400 REM SUBROUTINE TO HOLD OUTPUT ON SCREEN
1410 INPUT "FOR MORE OF TABLE PRESS ENTER";Z9
1420 GOSUB 1000
1430 RETURN
