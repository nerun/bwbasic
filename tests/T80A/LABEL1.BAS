1 REM LABEL1
5 CLEAR 1000
10 CLS:PRINT TAB(10);"SHIPPING LABEL MAKER":PRINT
20 PRINT"THIS SIMPLE PROGRAM WILL PRODUCE SHIPPING LABELS FOR AN ORDER
25 PRINT"CONSISTING OF MANY CARTONS. THE TOP LINE OF EACH LABEL HAS
30 PRINT"WRITTEN ON IT , E.G. 2 OF 3 P/O #45RT6, 3 OF 3 P/O #45RT6, ETC
35 PRINT"P/O # IS PURCHASE ORDER NUMBER. STANDARD 1 INCH LABELS ARE 
40 PRINT"USED.
90 PRINT:INPUT "PRESS ENTER TO BEGIN";Z9$
100 CLS
110 INPUT "HOW MANY LABELS DO YOU NEED     ";A
120 LINE INPUT "ENTER PURCHASE ORDER NUMBER ";P$
130 LINE INPUT "INPUT NAME                  ";N$
140 LINE INPUT "INPUT COMPANY(ENTER IF NONE)";CMP$
150 LINE INPUT "INPUT STREET ADDRESS        ";A$
160 LINE INPUT "INPUT CITY,STATE ZIP        ";C$
190 PRINT
200 INPUT "PRESS ENTER WHEN PRINTER IS READY";Z9$
210 INPUT "DO YOU WANT A MASK(Y/N)";Z9$
220 IF Z9$="Y" THEN FOR L=1 TO 6:LPRINT STRING$(30,"#"):NEXT:GOTO 190
230 FOR L=1 TO A
240 LPRINT L;"OF";A;"P/O #";P$
250 LPRINT N$
260 IF CMP$<>"" THEN LPRINT CMP$
270 LPRINT A$
280 LPRINT C$
290 LPRINT" ":IF CMP$="" THEN LPRINT " "
300 NEXT L
400 PRINT
410 INPUT "PRESS ENTER FOR MORE LABELS";Z9$
415 IF Z9$ = "-1" THEN END
420 GOTO 100
